import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

if(rootProject.file("keystore.properties").exists()) {
    // From https://developer.android.com/studio/publish/app-signing.html
    // to load the signing config file
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
   }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.jmstudios.redmoon"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 38
        versionName "3.5.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            def repo = Grgit.open(currentDir: project.rootDir)
            def commitId = "${repo.log().get(0).getAbbreviatedId()}"
            if (repo.status().isClean()) {
                versionNameSuffix "-$commitId"
            } else {
                versionNameSuffix "-$commitId (dirty)"
            }
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'NewApi','ExpiredTargetSdkVersion'
    }
}

dependencies {
    // Android/platform deps
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Written by me
    implementation "me.smichel.android:kpreferences:0.7.0"
    implementation project(":timepickerpreference")

    // 3rd party
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.github.topjohnwu.libsu:core:3.1.1'
    // annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'

}

repositories {
    mavenCentral()
}
